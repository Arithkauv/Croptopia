import net.fabricmc.loom.task.RemapJarTask

plugins {
    id 'fabric-loom' version '0.6-SNAPSHOT'
    id 'com.github.johnrengelman.shadow' version '4.0.4'
    id 'maven-publish'
}

repositories {
    mavenCentral()
    maven {
        url = 'https://maven.blamejared.com'
    }
}

rootProject.platform_tag_key = "c"

configurations {
    shade
    compile.extendsFrom shade
}

dependencies {
    minecraft "com.mojang:minecraft:${rootProject.mc_ver}"
    mappings "net.fabricmc:yarn:${rootProject.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${rootProject.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_version}"
    modImplementation "vazkii.patchouli:Patchouli:1.16.4-48-FABRIC"

    modImplementation ("net.dehydration:dehydration:${rootProject.dehydration_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    modCompile "io.github.prospector:modmenu:${rootProject.modmenu_version}",{
        exclude(group: "net.fabricmc.fabric-api")
    }


    shade project(":common")
}

processResources {
    inputs.property "mod_version", rootProject.version

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand "mod_version": rootProject.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

shadowJar {
    archiveFileName = rootProject.archives_base_name + "-${rootProject.mc_ver}-FABRIC-dev.jar"
    configurations = [project.configurations.shade]
}

task remappedShadowJar(type: RemapJarTask) {
    dependsOn tasks.shadowJar
    input = tasks.shadowJar.archiveFile
    addNestedDependencies = true
    archiveFileName = rootProject.archives_base_name + "-${rootProject.mc_ver}-FABRIC-${rootProject.version}.jar"
}

tasks.assemble.dependsOn tasks.remappedShadowJar

artifacts {
    archives remappedShadowJar
    shadow shadowJar
}