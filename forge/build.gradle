buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url= 'https://repo.spongepowered.org/repository/maven-public/' }
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1+', changing: true
        classpath 'gradle.plugin.com.github.jengelman.gradle.plugins:shadow:7.0.0'
        classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7-SNAPSHOT'
    }
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'

repositories {
    maven {
        url = 'https://maven.blamejared.com'
    }
}

archivesBaseName = archives_base_name + "-" + mc_ver + "-FORGE"

configurations {
    shade
    compileOnly.extendsFrom shade
    compileOnly.extendsFrom shadeSource
}


mixin {
    add sourceSets.main, "croptopia.refmap.json"
    config "croptopia.mixins.json"
}

sourceSets {
    main {
        resources {
            srcDirs += [
                    "src/main/generated",
                    rootProject.sourceSets.main.resources
            ]
        }
    }
}

minecraft {
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
    mappings channel: 'official', version: "${mc_ver}"
    runs {
        client {
            workingDirectory project.file('run')
            property 'legacyClassPath', '{minecraft_classpath}'
            arg "-mixin.config=croptopia.mixins.json"
            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            // Recommended logging level for the console //property 'forge.logging.console.level', 'debug'
            args '--mod', 'croptopia', '--all', '--output', file('src/generated/resources/'), '--existing', rootProject.file('src/main/resources/')
            mods {
                croptopia {
                    sources(sourceSets.main, rootProject.sourceSets.main)
                }
            }
        }

        server {
            workingDirectory project.file('run')
            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
            mods {
                croptopia {
                    sources(sourceSets.main, rootProject.sourceSets.main)
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'legacyClassPath', '{minecraft_classpath}'
            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
            args '--mod', 'croptopia', '--all', '--output', file('src/generated/resources/'), '--existing', rootProject.file('src/main/resources/')
            mods {
                croptopia {
                    sources(sourceSets.main, rootProject.sourceSets.main)
                }
            }
        }
    }
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    //from(sourceSets.main.resources.srcDirs) {
    //    include('**/data/croptopia/recipes/**')
    //    expand "dependent": "forge"
    //}

    from(sourceSets.main.resources.srcDirs) {
        include('**/data/**')
        include('**/dependents/**')
        expand "dependent": "forge"
        duplicatesStrategy = DuplicatesStrategy.INCLUDE
    }
}


dependencies {
    minecraft "net.minecraftforge:forge:${mc_ver}-${forge_ver}"
    implementation(rootProject)
    shade(rootProject)
    compileOnly fg.deobf("vazkii.patchouli:Patchouli:1.19-74-SNAPSHOT")
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

shadowJar {
    archiveClassifier = ""
    enabled = true
    configurations = [project.configurations.shade]
    //archiveFileName = rootProject.archives_base_name + "-${project.mc_ver}-FORGE-${rootProject.version}.jar"
    //destinationDirectory = new File("${rootProject.projectDir}/build/jars")
    relocate 'dependents/platform', 'data/forge'
}

artifacts {
    archives shadowJar
    shadow shadowJar
}

jar {
    //archiveFileName = rootProject.archives_base_name + "-${project.mc_ver}-FORGE-${rootProject.version}.jar"
    //destinationDirectory = new File("${rootProject.projectDir}/build/jars")
    manifest {
        attributes([
                "Specification-Title": "croptopia",
                "Specification-Vendor": "thonk",
                "Specification-Version": "1",
                "Implementation-Title": "${archives_base_name}",
                "Implementation-Version": "${mod_version}",
                "Implementation-Vendor" :"me thonk",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs": "croptopia.mixins.json"
        ])
    }
}

tasks.withType(JavaCompile) {
    doLast {
        copy {
            from "src/main/resources/dependents/platform"
            include "**/**/*.json"
            expand "dependent": "forge"
            into "${buildDir}/resources/main/data/forge/"
        }
    }
}

tasks.build.dependsOn(tasks.shadowJar)

reobf {
    shadowJar {}
}

jar.finalizedBy('reobfJar')

//tasks.getByName("addRefMapToShadowJar").mustRunAfter(task("shadowJar"))
