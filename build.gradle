plugins {
    id 'java'
    id 'org.spongepowered.gradle.vanilla' version '0.2.1-SNAPSHOT'
    id 'net.minecraftforge.gradle' version '[6.0,6.2)' apply(false)
    id 'org.spongepowered.mixin' version "0.7-SNAPSHOT" apply(false)
}

/*apply plugin: 'java'*/

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    version = mod_version + "-" + mc_ver
    group = maven_group

    repositories {
        mavenLocal()
    }
}

subprojects {

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    repositories {
        mavenCentral()
        maven { url "https://repo.spongepowered.org/repository/maven-public/"}
    }

    tasks.withType(GenerateModuleMetadata).configureEach {
        enabled = false
    }
}

minecraft {
    version(mc_ver)
}

dependencies {
    compileOnly group: 'org.spongepowered', name: 'mixin', version: '0.8.5'
    compileOnlyApi('org.jetbrains:annotations:23.0.0')
    compileOnlyApi('com.epherical.epherolib:EpheroLib:1.1.0-1.20.1')
}

sourceSets {
    main {
        resources {
            srcDirs += [
                    "src/main/generated"
            ]
        }
    }
}

task sourceJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = "${archives_base_name}"
            //version = version + "-${mc_ver}"
            // add all the jars that should be included when publishing to maven
            from (rootProject.components.java)

            artifact sourceJar
        }
    }

    repositories {
        maven {
            name = "EphericalMaven"
            url = "https://maven.epherical.com/releases"
            credentials {
                username = m_user
                password = m_pass
            }
        }
    }
}
